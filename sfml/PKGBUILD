# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Ondrej Martinak <omartinak@gmail.com>

pkgname=sfml
pkgver=3.0.1
pkgrel=1
pkgdesc='A simple, fast, cross-platform, and object-oriented multimedia API'
arch=('x86_64' 'powerpc' 'powerpc64' 'powerpc64le' 'espresso')
url='http://www.sfml-dev.org/'
license=('Zlib')
depends=('libsndfile' 'libxrandr' 'glew' 'freetype2' 'libx11' 'libxcursor' 'libxi')
makedepends=('mesa' 'cmake' 'doxygen' 'systemd' 'ninja')
provides=('libsfml-window.so' 'libsfml-network.so' 'libsfml-system.so'
          'libsfml-audio.so' 'libsfml-graphics.so')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/SFML/SFML/archive/${pkgver}.tar.gz)
sha512sums=('cee56eef93728aef9b361f8b1080572532ce5247c80a9d1be53b30dab4d67e8588316890555d4c4ba423e87b09f954327d70615e1ba72d458d8238fa84e589f9')

build() {
  export CXXFLAGS+=" -ffat-lto-objects"

  cmake \
    -B build \
    -S SFML-${pkgver} \
    -G Ninja \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSFML_USE_SYSTEM_DEPS=ON \
    -DSFML_BUILD_EXAMPLES=ON \
    -DSFML_BUILD_DOC=ON \
    -DSFML_PKGCONFIG_INSTALL_DIR=/usr/lib/pkgconfig \
    -DSFML_INSTALL_PKGCONFIG_FILES=ON
  ninja -C build
  ninja -C build doc
}

package() {
  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 SFML-${pkgver}/license.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

apb_extra_repos=(
  "extra-any::https://repo.wii-linux.org/arch/extra/any"
  "extra::https://repo.wii-linux.org/arch/extra/$arch"
)

