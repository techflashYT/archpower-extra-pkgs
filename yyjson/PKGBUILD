# Maintainer: Robin Candau <antiz@archlinux.org>
# Contributor: Mahdi Sarikhani <mahdisarikhani@outlook.com>
# Contributor: Vincent Bernardoff <vb@luminar.eu.org>

pkgname=yyjson
pkgver=0.12.0
pkgrel=1
pkgdesc="A high performance JSON library written in ANSI C"
arch=('x86_64' 'powerpc' 'powerpc64' 'powerpc64le' 'espresso')
url="https://ibireme.github.io/yyjson"
license=('MIT')
depends=('glibc')
makedepends=('cmake')
provides=('libyyjson.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ibireme/yyjson/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('b16246f617b2a136c78d73e5e2647c6f1de1313e46678062985bdcf1f40bb75d')

build() {
	cmake -B build -S "${pkgname}-${pkgver}" \
	  -DCMAKE_BUILD_TYPE='None' \
	  -DCMAKE_INSTALL_PREFIX='/usr' \
	  -DBUILD_SHARED_LIBS='ON' \
	  -DYYJSON_BUILD_TESTS='ON' \
	  -Wno-dev
	cmake --build build
}

check() {
	ctest --test-dir build --output-on-failure
}

package() {
	DESTDIR="${pkgdir}" cmake --install build

	cd "${pkgname}-${pkgver}"
	install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
	install -Dm 644 doc/*.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

apb_extra_repos=(
  "extra-any::https://repo.wii-linux.org/arch/extra/any"
  "extra::https://repo.wii-linux.org/arch/extra/$arch"
)

