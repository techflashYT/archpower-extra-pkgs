# Contributor: Balló György <ballogyor+arch at gmail dot com>

_pkgname=argon2_cffi
pkgname=python-argon2_cffi
pkgver=23.1.0
pkgrel=3
pkgdesc="CFFI-based Argon2 Bindings for Python"
arch=('any')
url="https://github.com/hynek/argon2_cffi"
license=('MIT')
depends=(python-argon2-cffi-bindings)
makedepends=(git python-hatch-vcs python-hatch-fancy-pypi-readme python-build python-installer)
source=("git+https://github.com/hynek/argon2_cffi?signed#tag=$pkgver")
sha256sums=('b5cbd90c2f4782604e5e2571f8aaf6818d7466460fe8d63cc2db8ddbc06dd34d')
validpgpkeys=('C2A04F86ACE28ADCF817DBB7AE2536227F69F181') # Hynek Schlawack

build() {
  cd $_pkgname
  python -m build --wheel --no-isolation
}

package() {
  cd $_pkgname
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
