# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: T.J. Townsend <blakkheim@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=scrot
pkgver=1.12.1
pkgrel=1
pkgdesc='Simple command-line screenshot utility for X'
url='https://github.com/resurrecting-open-source-projects/scrot'
arch=('x86_64' 'powerpc' 'powerpc64' 'powerpc64le' 'espresso')
license=('MIT')
depends=('imlib2' 'libxext' 'libxinerama' 'libxfixes' 'libxcomposite' 'libbsd')
makedepends=('autoconf-archive' 'git')
source=("git+https://github.com/resurrecting-open-source-projects/scrot.git#tag=${pkgver}")
sha512sums=('fdda244965347c3e760b95799f800ac67f18de434205d8d762ff61a08c3f9c8033e2d704d443f0b95b72c0b3e1beb6a342107eb5fe86405d2356289bcf0a93ee')
b2sums=('e453888c8c337d11686697e87e759f54bbe8854035298a6d1a4f404d7f9bba6f27f47764cb5d311e6e8854a41d5c9cbe5be260ccadc0cbd1ad4235d86fbcf7e2')

prepare(){
  cd ${pkgname}
  autoreconf -fiv
}

build() {
  cd ${pkgname}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
  install -Dm644 README.md ChangeLog -t "${pkgdir}"/usr/share/doc/${pkgname}
  install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
