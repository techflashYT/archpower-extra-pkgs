# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dhead666 <myfoolishgames@gmail.com>

pkgname=tinyxml2
pkgver=11.0.0
pkgrel=1
pkgdesc='Simple XML parser made for easy integration'
arch=('x86_64' 'powerpc' 'powerpc64' 'powerpc64le')
url='https://github.com/leethomason/tinyxml2'
license=('LicenseRef-zlib')
depends=('glibc' 'gcc-libs')
makedepends=('git' 'cmake')
source=("$pkgname::git+$url#tag=$pkgver")
sha512sums=('f13b0fa564bce8c6cd7940c69e65472f23164d2815c4f81c6aefe6527e861468fe089fb9e86f93bdbd3b6007db63d2888508ce13ed840c51a3f72a323a20ec8b')
b2sums=('8c20892fdd705b4ed0acffb2729ffa9bb77c96e4af07a7108abea4374fa47fde3465798d60c7f0582bd056cdd083dcf61ba5a4301aeb9b385f93a0bd77ab5bfd')

build() {
  local cmake_options=(
    -B build
    -S "$pkgname"
    -D CMAKE_INSTALL_PREFIX=/usr
    -D tinyxml2_SHARED_LIBS=ON
  )

  cmake "${cmake_options[@]}"

  cmake --build build
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE.txt"
}

# vim: ts=2 sw=2 et:
