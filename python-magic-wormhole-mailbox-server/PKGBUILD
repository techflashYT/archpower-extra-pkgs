# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Dian M Fay <dian.m.fay@gmail.com>

pkgname=python-magic-wormhole-mailbox-server
pkgver=0.5.1
pkgrel=2
pkgdesc="the rendezvous/mailbox server for magic-wormhole clients "
arch=(any)
url="https://github.com/magic-wormhole/magic-wormhole-mailbox-server"
license=(MIT)
depends=(python-autobahn
         python-twisted)
makedepends=(python-{build,installer,wheel}
             python-setuptools
             python-versioneer)
#checkdepends=(python-pytest
#              python-treq)
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('0e67f76da27bad404a23417a8b8080503cfbf5e142bd6f6df7def38b6a8851bc950e98e2b481a729d5342076af883e69c8628c9a1af5b09aa57caa7af72f6b49')
b2sums=('ef2dfb69e8dc9015c2ef252ccee13fcce3126a5193bf9b3fc54d38ef99a1f85e65271bc62803b8d85fc14dd45575a25f60afe4d50a6528e2b9a4bedd16bf5866')

prepare() {
	cd "${pkgname#python-}-$pkgver"
	# vendored versioneer is too old for Python 3.12
	rm -f versioneer.py
}

build() {
	cd "${pkgname#python-}-$pkgver"
	python -m build -wn
}

#check() {
#	cd "${pkgname#python-}-$pkgver"
#	export PYTHONPATH='src'
#	pytest
#}

package() {
	cd "${pkgname#python-}-$pkgver"
	python -m installer -d "$pkgdir" dist/*.whl
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname" docs/*.md
}
