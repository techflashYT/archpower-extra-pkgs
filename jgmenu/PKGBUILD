# Maintainer: Mortne Linderu <foxboron@archlinux.org>
# Contributor: Johan Malm <jgm323 at gmail dot com>

pkgname=jgmenu
pkgver=4.5.0
pkgrel=1
pkgdesc="Simple, independent, contemporary-looking X11 menu, designed for scripting, ricing and tweaking"
arch=('i686' 'x86_64' 'powerpc' 'powerpc64' 'powerpc64le')
url="https://www.github.com/johanmalm/jgmenu"
license=('GPL')
depends=('libx11' 'cairo' 'pango' 'libxrandr' 'librsvg' 'libxml2' 'glib2' 'menu-cache' 'python')
source=("${pkgname}-${pkgver}.tar.gz::https://www.github.com/johanmalm/jgmenu/archive/v${pkgver}.tar.gz")
sha256sums=('523d7f7a2225f4d2cc9772c44bcc661518d99ccc478bd0d099b0b157917304b1')

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr --with-lx --with-pmenu
	make
}

check(){
	cd "$pkgname-$pkgver"
	make test
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" prefix=/usr install
}
