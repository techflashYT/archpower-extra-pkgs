# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.6.2
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=('x86_64' 'powerpc' 'powerpc64' 'powerpc64le')
url='http://doomretro.com'
license=('GPL-3.0-only')
depends=(
  'glibc'
  'sdl2'
  'sdl2_image'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
sha512sums=('fb3271c45468ee90f0a16e60ad0ba3621f5c6978f088733d640237139d69a5a57cad6e9826d38f7c2387de38bf34097e31de376cf7f217de02809b9b94c83afe')
b2sums=('07ad3e861a534db807c8161ac34abd0018cd696f5570b890d7a84483782a6bdfea667a32d180b8a752a095becf73d5b5b98c3d891afbd1c461a9b65ea53627e4')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
