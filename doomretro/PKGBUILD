# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=doomretro
pkgver=5.7.2
pkgrel=1
pkgdesc='The classic, refined DOOM source port'
arch=('x86_64' 'powerpc' 'powerpc64' 'powerpc64le' 'espresso')
url='http://doomretro.com'
license=('GPL-3.0-only')
depends=(
  'glibc'
  'sdl2'
  'sdl2_image'
  'sdl2_mixer'
)
makedepends=('git' 'cmake')
optdepends=('timidity++: for music playback')
source=("$pkgname::git+https://github.com/bradharding/doomretro.git#tag=v$pkgver")
sha512sums=('ef025eac8daf34086dde3f499fb6e5b1150da7152a7c366bf71649e3827d36a7ffc47b430bb852ab146374c7dba2c3b6c9ef085a0313914aa7578c15db719685')
b2sums=('2a2f4127b72974e61f3e39b4a7b71ad5bebc6b3466ee2a39a88a2a2e9f7dfbc959ab778d62040b95cc35064ab59b24761b289c276762bdc9a00c3698595bc8aa')

build() {
  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
